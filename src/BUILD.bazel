load("//tools:ng.bzl", "ng_project")
load("//tools:sass.bzl", "sass_binary")
load("//tools:file_collector.bzl", "file_collector")
load("//:defs.bzl", "pkg_web")
load("@aspect_rules_js//js:defs.bzl", "js_run_devserver")
load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@aspect_rules_esbuild//esbuild:defs.bzl", "esbuild")
load("@npm//:history-server/package_json.bzl", history_server = "bin")
load("@npm//:tailwindcss/package_json.bzl", tailwindcss = "bin")

package(default_visibility = ["//:__pkg__"])

sass_binary(
    name = "styles",
    srcs = [
        "styles.scss",
        "syntax-highlighting.scss",
    ],
)

file_collector(
    name = "tailwind_content",
    extension = "html",
    deps = [":src"],
)

# without this styles.tailwind.css cannot be found by tailwindcss
copy_to_bin(
    name = "tailwindcss_bin_srcs",
    srcs = [
        ":styles.tailwind.css",
        ":tailwind.config.js",
    ],
)

tailwindcss.tailwindcss(
    name = "tailwindcss",
    srcs = [
        ":styles.tailwind.css",
        ":tailwind.config.js",
        ":tailwind_content",
        ":tailwindcss_bin_srcs",
    ],
    args = [
        "--config",
        "$(rootpath :tailwind.config.js)",
        "--input",
        "$(rootpath :styles.tailwind.css)",
        "--",
        "$(rootpaths :tailwind_content)",
    ],
    copy_srcs_to_bin = False,
    execution_requirements = {
        "no-sandbox": "1",
    },
    stdout = "tailwind.css",
    tags = ["no-sandbox"],
    use_execroot_entry_point = False,
)

ng_project(
    name = "src",
    srcs = [
        "main.ts",
    ],
    deps = [
        "//:node_modules/@angular/common",
        "//:node_modules/@angular/core",
        "//:node_modules/@angular/forms",
        "//:node_modules/@angular/router",
        "//:node_modules/@types/node",
        "//src/app",
    ],
)

ng_project(
    name = "polyfills",
    srcs = ["polyfills.ts"],
    visibility = ["//visibility:private"],
    deps = ["//:node_modules/zone.js"],
)

esbuild(
    name = "polyfills-bundle",
    srcs = [":polyfills"],
    config = {
        "resolveExtensions": [
            ".mjs",
            ".js",
        ],
    },
    define = {"process.env.NODE_ENV": "'production'"},
    entry_point = "polyfills.js",
    format = "esm",
    metafile = False,
    minify = True,
)

[pkg_web(
    name = mode,
    assets = ["//src/assets"],
    entry_deps = [":src"],
    entry_point = "main.js",
    html_assets = [
        ":styles.css",
        ":tailwind.css",
    ],
    index_html = "main.html",
    production = False if mode == "dev" else True,
    script_assets = [
        ":polyfills-bundle.js",
    ],
) for mode in [
    "dev",
    "prod",
]]

js_run_devserver(
    name = "devserver",
    args = ["$(location :dev)"],
    data = [":dev", ":src"],
    tool = "//tools:http_server",
    tags = [
        "ibazel_live_reload",
        "ibazel_notify_changes",
    ],
)

# history_server.history_server_binary(
#     name = "devserver",
#     args = ["$(location :dev)"],
#     data = [":dev"],
#     tags = [
#         "ibazel_live_reload",
#         "ibazel_notify_changes",
#     ],
# )

history_server.history_server_binary(
    name = "prodserver",
    args = ["$(location :prod)"],
    data = [":prod"],
)
